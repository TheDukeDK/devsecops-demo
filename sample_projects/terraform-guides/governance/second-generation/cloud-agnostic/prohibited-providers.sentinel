# This policy uses the tfconfig import to prohibited specified providers
# across all modules

##### Imports #####
import "tfconfig"
import "strings"

##### Functions #####

# Prevent providers that are not allowed
validate_providers = func(prohibited_list) {

  validated = true

  # Iterate over all modules in the tfconfig import
  for tfconfig.module_paths as path {
    providers = tfconfig.module(path).providers
    # Iterate over providers of the module
    for providers as provider {
      if provider in prohibited_list {
        if length(path) is 0 {
          print("The root module has provider", provider,
                "from the prohibited list:", prohibited_list )
        } else {
          print("The module module." + strings.join(path, ".module."),
                "has provider", provider, "from the prohibited list:", prohibited_list )
        }

        validated = false
      } // end provider check
    } // end providers loop
  } // end module loop

  return validated
}

##### Lists #####

# List of prohibited providers
prohibited_list = [
  "external",
  "http",
]

##### Rules #####

# Call the validation function
providers_validated = validate_providers(prohibited_list)

# Main rule
main = rule {
  providers_validated
}
