# This policy uses the Sentinel tfplan import to require that
# all EC2 instances have instance types from an allowed list.
# While it could have used the common validate_attribute_in_list function,
# we did not use it to provide an alternative.

##### Imports #####

import "tfplan"
import "strings"

##### Imports #####

import "tfplan"
import "strings"

##### Functions #####

# Find all resources of a specific type from all modules using the tfplan import
find_resources_from_plan = func(type) {

  resources = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {
    # Iterate over the named resources of desired type in the module
    for tfplan.module(path).resources[type] else {} as name, instances {
      # Iterate over resource instances
      for instances as index, r {

        # Get the address of the instance
        if length(path) == 0 {
          # root module
          address = type + "." + name + "[" + string(index) + "]"
        } else {
          # non-root module
          address = "module." + strings.join(path, ".module.") + "." +
                    type + "." + name + "[" + string(index) + "]"
        }

        # Add the instance to resources map, setting the key to the address
        resources[address] = r
      }
    }
  }

  return resources
}

# Validate that all instances of a specified resource type being modified have
# a specified top-level attribute in a given list
validate_instance_types = func(allowed_types) {

  validated = true

  # Get all resource instances of the specified type
  resource_instances = find_resources_from_plan("aws_launch_configuration")

  # Loop through the resource instances
  for resource_instances as address, r {

    # Skip resource instances that are being destroyed
    # to avoid unnecessary policy violations.
    # Used to be: if length(r.diff) == 0
    if r.destroy and not r.requires_new {
      print("Skipping resource", address, "that is being destroyed.")
      continue
    }

    # Determine if the attribute is computed
    if r.diff["instance_type"].computed else false is true {
      print("Launch configuration", address,
            "has attribute, instance_type, that is computed.")
      # If you want computed values to cause the policy to fail,
      # uncomment the next line.
      # validated = false
    } else {
      # Validate that each instance has allowed value
      if r.applied.instance_type else "" not in allowed_types {
        print("Launch configuration", address,
              "has attribute instance type with value", r.applied.instance_type,
              "that is not in the allowed list:", allowed_types)
        validated = false
      }
    }

  }
  return validated
}

##### Lists #####

# Allowed EC2 Instance Types
allowed_types = [
  "t2.nano",
  "t2.micro",
  "t2.small",
  "t2.medium",
  "t2.large",
]

##### Rules #####

# Call the validation function
lcs_validated = validate_instance_types(allowed_types)

# Main rule
main = rule {
  lcs_validated
}
